plugins {
    id 'application'
    id 'checkstyle'
    id 'findbugs'
    id 'idea'
    id 'java'
}
checkstyle {
    toolVersion = "7.1"
}

sourceCompatibility = 8
version = '4.6.0-SNAPSHOT'
group = "de.stups.slottool"

mainClassName = "de.hhu.stups.plues.modelgenerator.Main"

repositories {
    jcenter()
    mavenCentral()
}

// https://simonharrer.wordpress.com/2013/09/17/building-command-line-apps-with-gradle/
run {
    if (project.hasProperty('args')) {
        args project.args.split('\\s+')
    }
}

dependencies {
    compile group: 'commons-cli', name: 'commons-cli', version: '1.3.1'
    compile 'org.jtwig:jtwig-core:5.58'
    compile 'org.hibernate:hibernate-core:5.2.8.Final'
    compile 'org.hibernate:hibernate-c3p0:5.2.8.Final'
    compile 'org.hibernate:hibernate-ehcache:5.2.8.Final'
    compile 'com.google.code.findbugs:annotations:3.0.1u2'

    runtime group: 'org.xerial', name: 'sqlite-jdbc', version: '3.16.1'
    runtime group: 'commons-cli', name: 'commons-cli', version: '1.3.1'

    // logging
    compile 'org.slf4j:slf4j-api:1.7.24' // MIT
    compile 'ch.qos.logback:logback-core:1.2.1' // EPL
    compile 'ch.qos.logback:logback-classic:1.2.1' // EPL

    testRuntime "org.objenesis:objenesis:2.5.1"
    testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

task buildStandaloneJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Model generator for timetable data',
                'Implementation-Version': version,
                'Main-Class': mainClassName
    }
    baseName = project.name + '-standalone'
    from {
        configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}
